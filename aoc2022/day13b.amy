
//========================================================================

#include "Vector.amy"
#include "algorithms.amy"
#include "LinkedList.amy"

//========================================================================

char[] line = input();
Vector<:char[]:> lines = new Vector<:char[]:> ();
while (line != null)
{
    lines.pushBack (line);
    line = input();
}

//========================================================================

class Element
{
    public field int is_list;
    public field int value;
    public field Vector<:Element:> list;
    constructor (int value)
    {
        this.is_list = 0;
        this.value = value;
        this.list = null;
    }
    constructor (Vector<:Element:> list)
    {
        this.is_list = 1;
        this.value = 0;
        this.list = list;
    }
    public method void switch_to_list (Vector<:Element:> list)
    {
        this.is_list = 1;
        this.value = 0;
        this.list = list;
    }
}

function void print (Element e)
{
    if (e.is_list)
    {
        print ('[');
        if (e.list.size > 0)
            print (e.list.data[0]);
        for (int i = 1; i < e.list.size; ++i)
        {
            print (','); print (e.list.data[i]);
        }
        print (']');
        return;
    }
    print (e.value);
}

//========================================================================

template <:type0, type1:>
class Pair
{
    public field type0 a;
    public field type1 b;
    constructor (type0 a, type1 b)
    {
        this.a = a;
        this.b = b;
    }
}

//========================================================================

// returns built element and the updated i value
function Pair<:Element, int:> build_element (char[] line, int i, int line_size)
{
    // end of the line
    // this should not happen
    if (i >= line_size)
        return null;
    // match list
    if (line[i] == '[')
    {
        ++i;
        // match comma separated values
        Vector<:Element:> sub_elements = new Vector<:Element:> ();
        while (line[i] != ']')
        {
            Pair<:Element, int:> p = build_element (line, i, line_size);
            sub_elements.pushBack (p.a);
            i = p.b;
            if (line[i] == ',')
                ++i;
            free (p);
        }
        ++i;
        Element e = new Element (sub_elements);
        return new Pair<:Element, int:> (e, i);
    }
    // match number
    Vector<:char:> str = new Vector<:char:> ();
    while (!(line[i] == ',' || line[i] == ']'))
    {
        str.pushBack (line[i]);
        ++i;
    }
    // i is now at , or ]
    str.pushBack ('\0');
    int value = stringToInt (str.data);
    return new Pair<:Element, int:> (new Element (value), i);
}

//========================================================================

int RIGHT_ORDER = 1;
int CONTINUE = -1;
int WRONG_ORDER = 0;

function int is_correct_order (Element a, Element b)
{
    // functions cannot access outter scopes -__-
    int RIGHT_ORDER = 1;
    int CONTINUE = -1;
    int WRONG_ORDER = 0;
    
    // both values are integers
    if (!a.is_list && !b.is_list)
    {
        // lower should be first
        if (a.value < b.value)
            return RIGHT_ORDER;
        elif (a.value > b.value)
            return WRONG_ORDER;
        return CONTINUE;
    }

    // both values are lists
    if (a.is_list && b.is_list)
    {
        for (int i = 0; i < a.list.size && i < b.list.size; ++i)
        {
            // check sub elements 
            int res = is_correct_order (a.list.data[i], b.list.data[i]);
            if (res == RIGHT_ORDER)
                return RIGHT_ORDER;
            if (res == WRONG_ORDER)
                return WRONG_ORDER;
            // else CONTINUE
        }
        // reaches here if numbers match
        // if left list is smaller -> right order
        if (a.list.size < b.list.size)
            return RIGHT_ORDER;
        // if left list is bigger -> wrong order
        if (a.list.size > b.list.size)
            return WRONG_ORDER;
        // if same length -> continue
        return CONTINUE;

    }

    // miss matching - turn num into list
    int wasANum = 0;
    if (!a.is_list)
    {
        wasANum = 1;
        Element newnum = new Element (a.value);
        a.switch_to_list (new Vector<:Element:> ());
        a.list.pushBack (newnum);
    }
    if (!b.is_list)
    {
        wasANum = 0;
        Element newnum = new Element (b.value);
        b.switch_to_list (new Vector<:Element:> ());
        b.list.pushBack (newnum);
    }
    // recheck
    int res = is_correct_order (a, b);

    // undo listification
    if (wasANum)
    {
        Element a_ = a.list.popBack ();
        a.is_list = 0;
        a.value = a_.value;
        free (a.list);
        free (a_);
    }
    else 
    {
        Element b_ = b.list.popBack ();
        b.is_list = 0;
        b.value = b_.value;
        free (b.list);
        free (b_);
    }

    return res;

}

//========================================================================

Vector<:Element:> elements = new Vector<:Element:> ();

// add [[2]]
Element e0 = new Element (2);
Vector<:Element:> e1 = new Vector<:Element:> ();
e1.pushBack (e0);
Element e2 = new Element (e1);
Vector<:Element:> e3 = new Vector<:Element:> ();
e3.pushBack (e2);
Element divider2 = new Element (e3);
elements.pushBack (divider2);

// add [[6]]
e0 = new Element (6);
e1 = new Vector<:Element:> ();
e1.pushBack (e0);
e2 = new Element (e1);
e3 = new Vector<:Element:> ();
e3.pushBack (e2);
Element divider6 = new Element (e3);
elements.pushBack (divider6);

for (int l = 0; l < lines.size; l+=1)
{
    //print (l+1); print ('/'); println (lines.size);
    if (lines.data[l][0] == '\n')
        continue;
    Pair<:Element, int:> p0 = build_element (lines.data[l+0], 0, strlen (lines.data[l+0])-1);
    //print (p0.a); println ();

    elements.pushBack (p0.a);
    free (p0);

}

//========================================================================

function void bubbleSort (Vector<:Element:> arr)
{
    // functions cannot access outter scopes -__-
    int RIGHT_ORDER = 1;
    int CONTINUE = -1;
    int WRONG_ORDER = 0;
    
    int hasSwapped = 1;
    int i = 0;
    while (hasSwapped)
    {
        //print (i++); print ('/'); println (arr.size);
        hasSwapped = 0;
        for (int i = 1; i < arr.size; ++i)
        {
            //print (i+1); print ('/'); println (arr.size);
            int res = is_correct_order (arr.data[i-1], arr.data[i]);
            if (res == WRONG_ORDER)
            {
                Element temp = arr.get(i-1);
                arr.set(i-1, arr.get(i));
                arr.set(i, temp);
                hasSwapped = 1;
            }
        }
    }
}

//========================================================================

bubbleSort (elements);

int divider2i = 0;
int divider6i = 0;
for (int i = 0; i < elements.size; ++i)
{
    if (elements.data[i] == divider2)
        divider2i = i+1;
    if (elements.data[i] == divider6)
        divider6i = i+1;
}

println (divider2i * divider6i);


